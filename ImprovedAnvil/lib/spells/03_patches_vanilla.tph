/// Patch existing vanilla spells ///

// common innates (spells) //

COPY_EXISTING ~spin535.spl~ ~override/spin535.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~SPIN658.spl~ ~override/SPIN658.spl~
  WRITE_SHORT 0x25      0 // type
  WRITE_BYTE  0x27    0xc // secondary type

COPY_EXISTING ~spin674.spl~ ~override/spin674.spl~
  WRITE_BYTE  0x25      4 // primary type

COPY_EXISTING ~SPIN695.spl~ ~override/SPIN695.spl~
  WRITE_SHORT 0x25      0 // type
  WRITE_BYTE  0x27    0xc // secondary type

COPY_EXISTING ~SPWI406.spl~ ~override/SPIN736.spl~
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    header_type = 1
    target = 2
  END

  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR
    header = 1
    target = 1
  END

COPY_EXISTING ~SPIN768.spl~ ~override/SPIN768.spl~
  WRITE_SHORT 0x25      0 // type
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~SPIN769.spl~ ~override/SPIN769.spl~
  WRITE_SHORT 0x25      0 // type
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spin775.spl~ ~override/spin775.spl~
  SAY NAME1 @358
  WRITE_BYTE  0x27    0xb // secondary type

COPY_EXISTING ~spin839.spl~ ~override/spin839.spl~
  WRITE_BYTE  0x25      4 // primary type

COPY_EXISTING ~SPIN890.spl~ ~override/SPIN890.spl~
  WRITE_BYTE  0x27    0xb // secondary type

COPY_EXISTING ~SPIN893.spl~ ~override/SPIN893.spl~
  WRITE_BYTE  0x27    0xa // secondary type

COPY_EXISTING ~spin894.spl~ ~override/spin894.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spin909.spl~ ~override/spin909.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spin910.spl~ ~override/spin910.spl~
  WRITE_BYTE  0x25      4 // primary type
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spin911.spl~ ~override/spin911.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spin975.spl~ ~override/spin975.spl~
  WRITE_BYTE  0x25      4 // primary type

COPY_EXISTING ~spin976.spl~ ~override/spin976.spl~
  WRITE_BYTE  0x25      4 // primary type
  WRITE_BYTE  0x27     11 // secondary type

COPY_EXISTING ~SPIN999.spl~ ~override/SPIN999.spl~
  WRITE_BYTE  0x25      4 // primary type
  WRITE_BYTE  0x27    0xb // secondary type

// class level abilities //

// Seeking Sword
COPY_EXISTING ~SPCL731.spl~ ~override/SPCL731.spl~
  SAY UNIDENTIFIED_DESC @13041

  // Set proper weapon per level
  PATCH_FOR_EACH ~ability_num~ IN "1" "2" "3" "4" "5" "6" "7" "8" "9" BEGIN
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 111
      STR_VAR
        resource = ~S!seek01~
    END
  END  

  PATCH_FOR_EACH ~ability_num~ IN "10" "11" BEGIN
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 111
      STR_VAR
        resource = ~S!seek02~
    END
  END  

  PATCH_FOR_EACH ~ability_num~ IN "12" "13" "14" BEGIN
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 111
      STR_VAR
        resource = ~S!seek03~
    END
  END 

  PATCH_FOR_EACH ~ability_num~ IN "15" "16" "17" "18" "19" BEGIN
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 111
      STR_VAR
        resource = ~S!seek04~
    END
  END 

  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
    INT_VAR
      header = 20
      match_opcode = 111
    STR_VAR
      resource = ~S!seek05~
  END

  LPF ~S!CLONE_SPELL_HEADER~
    INT_VAR
      header_to_copy = 19
      level_req = 25
  END

  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
    INT_VAR
      header = 21
      match_opcode = 111
    STR_VAR
      resource = ~S!seek06~
  END

  // Make the spell undispellable
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      resist_dispel = 0
  END

// Boon of Lathander
COPY_EXISTING ~SPCL741.spl~ ~override/SPCL741.spl~
  SAY UNIDENTIFIED_DESC @13040

  // Give additional +1 thaco/dmg from lvl 11 up
  PATCH_FOR_EACH ~ability_num~ IN "2" "3" "4" "5" "6" "7" "8" "9" "10" "11" BEGIN
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 73
        parameter1 = 2
    END

    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 54
        parameter1 = 2
    END
  END  

// Hold Undead Lathander ability
COPY_EXISTING ~SPCL742.spl~ ~override/SPCL742.spl~
  SAY UNIDENTIFIED_DESC @13009

  // remove all but one spell ability
  PATCH_FOR_EACH ~min_lvl_val~ IN "2" "3" "4" "5" "6" "7" "8" "9" "10" "11" "12" "13" "14" "15" "16" "17" "18" "19" BEGIN
    LAUNCH_PATCH_FUNCTION ~DELETE_SPELL_HEADER~ 
      INT_VAR
        header_type = ~-1~
        min_level = ~%min_lvl_val%~
    END
  END

  // change projectile to None, to make it single target
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile  = 1
  END

  // remove Hold EFF
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 185
  END

  // make all initial effects bypass MR and allow no saving throw
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      resist_dispel = 3
      savingthrow = 0
  END

  // Add effects slowing movements and reducing physical resistances with no saving throw
  PATCH_FOR_EACH ~eff_name~ IN "S!SLD-10" "S!CRD-10" "S!SLD-10" "S!CRD-10" "S!MVM50" BEGIN
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
      INT_VAR
        timing = 0
      STR_VAR
        match_resource = ~HOLDSND~
        resource = EVALUATE_BUFFER ~%eff_name%~
    END
  END

  // Add additional 10% of physical resistances reduction with no saving throw from level 20
  PATCH_FOR_EACH ~eff_name~ IN "S!SLD-10" "S!CRD-10" BEGIN
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
      INT_VAR
        header = 1
        timing = 0
      STR_VAR
        match_resource = ~HOLDSND~
        resource = EVALUATE_BUFFER ~%eff_name%~
    END
  END

  // Add effects slowing movements with saving throw
  LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
    INT_VAR
      savingthrow = 1
      savebonus = ~-2~
      timing = 0
    STR_VAR
      match_resource = ~HOLDSND~
      resource = "S!MVM0"
  END

  // prevent stacking
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 206
      target = 2
      resist_dispel = 3
      insert_point = ~-1~
    STR_VAR
      resource = ~SPCL742~
  END  

  // update duration for all effects
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = ~-1~
      duration = 18
  END


// Resist magic
COPY_EXISTING ~SPCL904.spl~ ~override/SPCL904.spl~
  WRITE_BYTE  0x27      2 // secondary type

// priest spells //

// patch clerical spells not to activate SCRIPTINGSTATE6 on a character
COPY_EXISTING ~BHAAL2A.spl~ ~override/BHAAL2A.spl~
              ~SPIN103.spl~ ~override/SPIN103.spl~
              ~SPPR202.spl~ ~override/SPPR202.spl~
              ~SPPR214.spl~ ~override/SPPR214.spl~
              ~SPPR412.spl~ ~override/SPPR412.spl~
              ~SPPR513.spl~ ~override/SPPR513.spl~
              ~DGRIGHT.spl~ ~override/DGRIGHT.spl~
  LPF ~DELETE_SPELL_EFFECT~ INT_VAR
    opcode_to_delete = 282
  END

// patch "wake on sleep" for Command and Greater Command
COPY_EXISTING ~SPPR102.spl~ ~override/SPPR102.spl~
              ~SPPR512.spl~ ~override/SPPR512.spl~
  LPF ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    match_opcode = 39
    parameter2 = 1
  END

// Bless
COPY_EXISTING ~SPPR101.spl~ ~override/SPPR101.spl~
  SAY UNIDENTIFIED_DESC @13017

  // add spell state
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 130
      opcode = 328
      parameter2 = 22
      special = 1
  END

  // increase duration to 1 turn
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      duration_high = 60
  END

// Spirit Ward
COPY_EXISTING ~SPPR150.spl~ ~override/SPPR150.spl~
  SAY UNIDENTIFIED_DESC @10267

  // remove attack roll penalty (219)
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 219 END

  // update save vs. school bonus (349)
  LPF ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    match_opcode = 346
    parameter1 = 6
  END

// Aid
COPY_EXISTING ~SPPR201.spl~ ~override/SPPR201.spl~
  SAY UNIDENTIFIED_DESC @13016

  // make the spell AoE from lvl 15
  PATCH_FOR_EACH ~spell_level~ IN "15" "16" "17" "18" "19" "20" BEGIN
    LPF ~ALTER_SPELL_HEADER~
      INT_VAR
        header = %spell_level%
        projectile = 162
    END
  END

// Draw Upon Holy Might
COPY_EXISTING ~SPPR214.spl~ ~override/SPPR214.spl~
  // add spell state
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 328
      parameter2 = 25
  END

  // Cast improved version if caster is Lathander
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 326
      insert_point = ~-1~
      target = 1
      power = 2
      parameter1 = 16405
      parameter2 = %kit_equals%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR214D"
  END

// Lathander Draw Upon Holy Might
COPY_EXISTING ~SPPR214.spl~ ~override/SPPR214D.spl~
  // Remove spell states to avoid AI problems
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 321
      target = 1
      insert_point = 0
      resist_dispel = 0
    STR_VAR
      resource = ~SPPR214~
  END

  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 328
  END

  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 326
  END

  // Make undispellable
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      resist_dispel = 0
  END

  // Increase duration to 90s
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      duration_high = 90
  END

// Strength of One
COPY_EXISTING ~SPPR312.spl~ ~override/SPPR312.spl~
  SAY UNIDENTIFIED_DESC @13038

  // Add immunity to this spell if strength is higher than 18/75
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = 0
      target = 2
      power = 3
      parameter2 = %str_higher_than_18_75%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR312"
  END

  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = 0
      target = 2
      power = 3
      parameter1 = 19
      parameter2 = 124
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR312"
  END

COPY_EXISTING ~SPPR318.spl~ ~override/SPPR318.spl~
  WRITE_SHORT  0x20      0 // Priest Type

// Spiritual Clarity
COPY_EXISTING ~SPPR350.spl~ ~override/SPPR350.spl~
  SAY UNIDENTIFIED_DESC @10268

  // update casting speed
  LPF ~ALTER_SPELL_HEADER~ INT_VAR speed = 3 END

  // add prot from envelop
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 321
    STR_VAR
      match_resource = ~SPPR550~
      resource = ~S!misva2.spl~
  END

  // remove stuff vs. fear
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 23 END
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 161 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2=36 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPIN105~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPWI125~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPWI205~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPWI811~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPIN895~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPPR706~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~SPPR416~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource=~BDAURAFE~ END

// Remove summon animals from Clerics
COPY_EXISTING ~SPPR402.spl~ ~override/SPPR402.spl~
              ~SPPR501.spl~ ~override/SPPR501.spl~
              ~SPPR602.spl~ ~override/SPPR602.spl~
              ~SPPR604.spl~ ~override/SPPR604.spl~
  WRITE_BYTE  0x21      0x40

// Defensive Harmony
COPY_EXISTING ~SPPR406.spl~ ~override/SPPR406.spl~
  SAY UNIDENTIFIED_DESC @13033

  // Cast improved version if caster is Cleric kit
  PATCH_FOR_EACH ~cleric_kit~ IN "16404" "16405" "16403" "16425" "16424" BEGIN
    LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
      INT_VAR
        opcode = 326
        insert_point = 10
        target = 9
        power = 4
        parameter1 = %cleric_kit%
        parameter2 = %kit_equals%
        duration = 37
        resist_dispel = 3
      STR_VAR
        resource = EVALUATE_BUFFER "SPPR406D"
    END
  END

// Holy Power
COPY_EXISTING ~SPPR412.spl~ ~override/SPPR412.spl~
  SAY UNIDENTIFIED_DESC @13039

  // Remove original strength bonus
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 44
  END

  // Cast improved version if caster is Tempus
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 326
      insert_point = ~-1~
      target = 1
      power = 4
      parameter1 = 16425
      parameter2 = %kit_equals%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR412D"
  END

  // Protect from last effect if str > 18
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = ~-1~
      target = 1
      power = 4
      parameter1 = 19
      parameter2 = 124
      duration = 1
      resist_dispel = 0
    STR_VAR
      resource = "SPPR412"
  END

  // Add strength bonus as the last effect
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 97
      opcode = 44
      parameter1 = 18
    STR_VAR
      insert = ~last~
  END

// Tempus Holy Power
COPY_EXISTING ~SPPR412.spl~ ~override/SPPR412D.spl~
  // Remove spell states to avoid AI problems
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 328
  END

  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 326
  END

  // Make undispellable
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      resist_dispel = 0
  END

  // Add protection from self
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = 318
    STR_VAR
      resource = ~SPPR412D~
  END

  // Add 1/2 APR for levels 12-19
  PATCH_FOR_EACH ~ability_num~ IN "5" "6" "7" "8" "9" "10" "11" "12" BEGIN
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
      INT_VAR
        header = %ability_num%
        match_opcode = 54
        opcode = 1
        parameter1 = 6
        parameter2 = 0
    END
  END

  // Add 1 APR from level 20
  LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
    INT_VAR
      header = 13
      match_opcode = 54
      opcode = 1
      parameter1 = 1
      parameter2 = 0
  END

// Cause Serious Wounds
COPY_EXISTING ~SPPR414.spl~ ~override/SPPR414.spl~
  SAY UNIDENTIFIED_DESC @13003

  // Update casting speed
  LPF ~ALTER_SPELL_HEADER~ INT_VAR speed = 2 END

  // Increase duration to 60s
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = 111
      duration = 60
  END

  // Improve the spell from lvl 15
  LPF ~S!CLONE_SPELL_HEADER~
    INT_VAR
      header_to_copy = 0
      level_req = 15
  END

  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      header = 2
      match_opcode = 111
    STR_VAR
      resource = ~S!serio1~
  END

  // Improve the spell from lvl 20
  LPF ~S!CLONE_SPELL_HEADER~
    INT_VAR
      header_to_copy = 0
      level_req = 20
  END

  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      header = 3
      match_opcode = 111
    STR_VAR
      resource = ~S!serio2~
  END

// Spirit Fire
COPY_EXISTING ~SPPR450.spl~ ~override/SPPR450.spl~
  SAY UNIDENTIFIED_DESC @10323

  // update cast spell (146)
  LPF ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    match_opcode = 146
    probability1 = 100
    savingthrow = 0
  END

// Iron Skins
COPY_EXISTING ~sppr506.spl~ ~override/sppr506.spl~
  SAY UNIDENTIFIED_DESC @10189
  WRITE_BYTE  0x27      7 // secondary type

  // patch casting time
  READ_SHORT 0x68 "icount"
  READ_LONG 0x64 "ioffset"
  SET "it" = 0
  WHILE ("%it%" < "%icount%") BEGIN
    WRITE_SHORT (%ioffset% + ("%it%" * 0x28) + 0x12) 1
    "it" = "%it%" + 1
  END

// Cause Critical Wounds
COPY_EXISTING ~SPPR510.spl~ ~override/SPPR510.spl~
  SAY UNIDENTIFIED_DESC @13004

  // Update casting speed
  LPF ~ALTER_SPELL_HEADER~ INT_VAR speed = 3 END

  // Increase duration to 60s
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = 111
      duration = 60
  END

  // Improve the spell from lvl 15
  LPF ~S!CLONE_SPELL_HEADER~
    INT_VAR
      header_to_copy = 0
      level_req = 15
  END

  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      header = 2
      match_opcode = 111
    STR_VAR
      resource = ~S!criti1~
  END

  // Improve the spell from lvl 20
  LPF ~S!CLONE_SPELL_HEADER~
    INT_VAR
      header_to_copy = 0
      level_req = 20
  END

  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      header = 3
      match_opcode = 111
    STR_VAR
      resource = ~S!criti2~
  END

// Righteous Magic
COPY_EXISTING ~SPPR513.spl~ ~override/SPPR513.spl~
  // Add spell state
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 328
      parameter2 = ~%righteous_magic_state%~
  END

  // Cast improved version if caster is Tempus
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 326
      insert_point = ~-1~
      target = 1
      power = 5
      parameter1 = 16425
      parameter2 = %kit_equals%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR513D"
  END

// Tempus Righteous Magic
COPY_EXISTING ~SPPR513.spl~ ~override/SPPR513D.spl~
  // Remove original Righteous Might spell
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 321
      target = 1
      insert_point = 0
      resist_dispel = 0
    STR_VAR
      resource = ~SPPR513~
  END

  // Remove spell states to avoid AI problems
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 328
  END

  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 326
  END

  // Make undispellable
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      resist_dispel = 0
  END

// Weaken Undead
COPY_EXISTING ~SPPR515.spl~ ~override/SPPR515.spl~
  SAY NAME1 @13000
  SAY UNIDENTIFIED_DESC @13001

  // Update duration for all effects
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = ~-1~
      duration = 36
  END

  // Update icon effect to bypass MR
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 142
      resist_dispel = 2
  END

  // Remove original "cast spell on condition", since it's affected by slow/haste
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 232
  END

  // Make the spell apply its effects every 6s like True Sight
  PATCH_FOR_EACH ~weaken_wave~ IN "0" "1" "2" "3" "4" "5" "6" BEGIN
    LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
      INT_VAR
        opcode = 146
        target = 1
        parameter2 = 1
        resist_dispel = 2
        timing = 3
        duration = 6 * %weaken_wave%
        power = 5
      STR_VAR
        resource = ~SPPR515D~
    END
  END

// Weaken Undead subspell
COPY_EXISTING ~SPPR515D.spl~ ~override/SPPR515D.spl~
  SAY NAME1 @13000
  SAY UNIDENTIFIED_DESC @13001

  // Update duration for all effects
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = ~-1~
      duration = 38
  END

  // Remove previous instance of this spell to prevent stacking
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 321
      target = 2
      insert_point = 0
      resist_dispel = 3
    STR_VAR
      resource = ~SPPR515D~
  END

  // Delete Wing Buffet effect
  LPF ~DELETE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
    STR_VAR
      match_resource = ~REPDEAD~
  END

  // Modify visual effect
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      duration = 7
      timing = 0
      resist_dispel = 3
    STR_VAR
      match_resource = ~REPVISU~
  END

  // Modify sound effect
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      resist_dispel = 3
      timing = 0
    STR_VAR
      match_resource = ~COLDHIT~
  END

  // Add weakening effects
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!CRTHDC~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!SLDMDC~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!CRDMDC~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!PIDMDC~
  END  

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!MIDMDC~
  END
  
// Recall Spirit
COPY_EXISTING ~SPPR550.spl~ ~override/SPPR550.spl~
  SAY UNIDENTIFIED_DESC @10324

  // Update casting speed
  LPF ~ALTER_SPELL_HEADER~ INT_VAR speed = 6 END

// False Dawn
COPY_EXISTING ~SPPR609.spl~ ~override/SPPR609.spl~
  SAY UNIDENTIFIED_DESC @13031

  LPF ~ALTER_SPELL_HEADER~ INT_VAR projectile = ~%S!FALSED%~ END
  
  // Modify current effects to bypass magic resistance
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      resist_dispel = 2
      timing = 0
      parameter1 = 125
      parameter2 = 4
  END

  // Add another effect, which sets fire resistance to 0
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      timing = 0
    STR_VAR
      match_resource = ~DAWNDAM~
      resource = ~S!0frres~
  END

  // Clone all effects and make them work only on Shadows
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      parameter1 = 132
    STR_VAR
      match_resource = ~S!0frres~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      parameter1 = 132
    STR_VAR
      match_resource = ~DAWNDAM~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      parameter1 = 132
    STR_VAR
      match_resource = ~DAWNVIS2~
  END

// Bolt of Glory
COPY_EXISTING ~SPPR612.spl~ ~override/SPPR612.spl~
  SAY UNIDENTIFIED_DESC @13032

  // add effect, which sets magic damage resistance of demons to 60% of their current value
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      timing = 0
    STR_VAR
      match_resource = ~BOLTDEM~
      resource = ~S!60mres~
  END

COPY_EXISTING ~SPPR705.spl~ ~override/SPPR705.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

// Sunray
COPY_EXISTING ~SPPR707.spl~ ~override/SPPR707.spl~
  SAY UNIDENTIFIED_DESC @13037

  // Remove level progression from the spell
  PATCH_FOR_EACH ~min_lvl_val~ IN "15" "16" "17" "18" "19" "20" BEGIN
    LAUNCH_PATCH_FUNCTION ~DELETE_SPELL_HEADER~ 
      INT_VAR
        header_type = ~-1~
        min_level = ~%min_lvl_val%~
    END
  END

  // Change damage from magic to fire
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ 
    INT_VAR
      match_opcode  = 12
      parameter2 = 0x00080000
  END

  // Remove original effects to undead
  LAUNCH_PATCH_FUNCTION ~DELETE_SPELL_EFFECT~ 
    INT_VAR
      opcode_to_delete = 177
  END

  // -25% of current fire resistance to Undead
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 177
      insert_point = 0
      target = 2
      power = 7
      parameter1 = 4
      parameter2 = 3
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "S!75fres"
  END

  // Additional damage to undead
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 177
      insert_point = 1
      target = 2
      power = 7
      parameter1 = 4
      parameter2 = 3
      timing = 1
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "S!sunray"
  END

  // Add immunity to this spell if fire res > 100
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = 0
      target = 2
      power = 7
      parameter1 = 100
      parameter2 = %fire_res_higher%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR707"
  END

COPY_EXISTING ~SPPR709.spl~ ~override/SPPR709.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~sppr710.spl~ ~override/sppr710.spl~
  WRITE_BYTE  0x27    0xc // secondary type
  WRITE_BYTE  0x72    2 // change type to ranged
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR
    header = 1
    projectile = 209
    range = 15
  END

COPY_EXISTING ~sppr715.spl~ ~override/sppr715.spl~
  WRITE_BYTE  0x27    0xc // secondary type

COPY_EXISTING ~sppr983.spl~ ~override/sppr983.spl~
  WRITE_BYTE  0x25      4 // primary type
  WRITE_BYTE  0x27     11 // secondary type

COPY_EXISTING ~sppr989.spl~ ~override/sppr989.spl~
  WRITE_BYTE  0x25      4 // primary type
  WRITE_BYTE  0x27     11 // secondary type

// wizard spells //

COPY_EXISTING ~SPWI205.spl~ ~override/SPWI205.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~SPWI213.spl~ ~override/SPWI213.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

// remove penalty to saving-throws for display string effect, Ray of Enfeeblement
COPY_EXISTING ~SPWI221.spl~ ~override/spwi221.spl~
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    header_type = 2
    match_opcode = 139
    savebonus = 0
  END

// remove 'Slow' portrait icon with the Haste spell
COPY_EXISTING ~SPWI305.spl~ ~override/SPWI305.spl~
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 240
      target = 2
      power = 3
      parameter2 = 41
      timing = 1
      resist_dispel = 3
  END

// remove bonus to saving-throws for play visual effect, Dire Charm
COPY_EXISTING ~SPWI316.spl~ ~override/SPWI316.spl~
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    header_type = 1
    match_opcode = 215
    savebonus = 0
  END

COPY_EXISTING ~SPWI401.spl~ ~override/SPWI401.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~SPWI411.spl~ ~override/SPWI411.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

  // patch "wake on sleep" for Emotion
  LPF ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    match_opcode = 39
    parameter2 = 1
  END

// Update Breach to its own new secondary type
COPY_EXISTING ~spwi513.spl~  ~override/spwi513.spl~
              ~spwish38.spl~ ~override/spwish38.spl~
  WRITE_BYTE  0x27 ~%Breach%~ // secondary type

COPY_EXISTING ~spwi516.spl~ ~override/spwi516.spl~
  WRITE_BYTE  0x27      0 // secondary type

// Update Spell Shield to protect from Breach
COPY_EXISTING ~spwi519.spl~ ~override/spwi519.spl~
              ~spwii01.spl~ ~override/spwii01.spl~
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 226
      parameter2 = ~%Breach%~
    STR_VAR
      insert = ~below~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 226
      match_parameter2 = ~%Breach%~
      opcode = 328
      parameter1 = 0
      parameter2 = 108
      special = 1
    STR_VAR
      insert = ~below~
  END

COPY_EXISTING ~spwi520.spl~ ~override/spwi520.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spwi521.spl~ ~override/spwi521.spl~
  WRITE_BYTE  0x27      0 // secondary type

// Update Spell Turning to protect from Breach
COPY_EXISTING ~spwi701.spl~ ~override/spwi701.spl~
              ~spwii03.spl~ ~override/spwii03.spl~
              ~spwii24.spl~ ~override/spwii24.spl~
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 200
      match_parameter2 = 9
      opcode = 228
      parameter2 = ~%Breach%~
    STR_VAR
      insert = ~below~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 228
      opcode = 328
      parameter1 = 0
      parameter2 = ~%spell_turning_state%~
      special = 1
    STR_VAR
      insert = ~below~
  END

// patch the display portrait icon value for PfME to point to the proper icon
// Spell Shield is hardcoded to display 73, PfME points there by default as well
// states.bam and states2.bam are provided with proper icons at both frames
COPY_EXISTING ~SPWI606.spl~ ~override/spwi606.spl~
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    header_type = 1
    match_opcode = 142
    parameter2 = 123
  END
  BUT_ONLY

COPY_EXISTING ~SPWI612.spl~ ~override/SPWI612.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~spwi620.spl~ ~override/spwi620.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spwi621.spl~ ~override/spwi621.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~spwi622.spl~ ~override/spwi622.spl~
  WRITE_BYTE  0x27      0 // secondary type

COPY_EXISTING ~SPWI715.spl~ ~override/SPWI715.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~SPWI805.spl~ ~override/SPWI805.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~SPWI910.spl~ ~override/SPWI910.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~SPWI911.spl~ ~override/SPWI911.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

COPY_EXISTING ~SPWI912.spl~ ~override/SPWI912.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

// wish spells //

// patch projectile type for Wish:Rest option from Invisible Travelling (79) to None (1)
COPY_EXISTING ~SPWISH16.spl~ ~override/SPWISH16.spl~
  LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR
    header = 1
    projectile = 1
  END
  BUT_ONLY

COPY_EXISTING ~SPWISH35.spl~ ~override/SPWISH35.spl~
  READ_BYTE 0x19 "splflags"
  WRITE_BYTE 0x19 ("%splflags%" BOR 0b00000100)

// various named spells //

COPY_EXISTING ~BEGUILE.spl~ ~override/BEGUILE.spl~
  WRITE_BYTE  0x25    4   // primary type
  WRITE_BYTE  0x27    0xb // secondary type

COPY_EXISTING ~melstone.spl~ ~override/melstone.spl~
  WRITE_BYTE  0x27      7 // secondary type

COPY_EXISTING ~METSWARM.pro~ ~override/METSWARM.pro~
  WRITE_BYTE 0x216 4

COPY_EXISTING ~SPWII11.spl~ ~override/SPWII11.spl~
  WRITE_BYTE  0x25      4 // primary type

// projectiles //

// fix Blade Barrier's projectile to affect only enemies
COPY_EXISTING ~SMLLARNC.pro~ ~override/SMLLARNC.pro~
  READ_LONG 0x00200 "areaflags"
  WRITE_LONG 0x00200 ("%areaflags%" | BIT6)
  BUT_ONLY

// fix Comet's projectile to affect only enemies
COPY_EXISTING ~comet.pro~ ~override/comet.pro~
  READ_LONG 0x00200 "areaflags"
  WRITE_LONG 0x00200 ("%areaflags%" | BIT6)
  BUT_ONLY

// fix Writhing Fog's projectile to affect only allies, caster included
COPY_EXISTING ~writhing.pro~ ~override/writhing.pro~
  READ_LONG  0x0c behavior
  WRITE_LONG 0x0c ("%behavior%" ^^ BIT4)

  READ_LONG 0x00200 "areaflags"
  WRITE_LONG 0x00200 ("%areaflags%" | BIT6 | BIT7)

  WRITE_BYTE 0x00216 4 // trigger count

  BUT_ONLY

// @todo move this list to a separate .ids file
// patch in removal of shaman's innate aura cleansing, cause X wounds and purify aura

COPY_EXISTING_REGEXP GLOB ~SPPR[1-7][0-5][0-9]+\.spl~ ~override~
                          ~SPWI[1-9][0-9][0-9]+\.spl~ ~override~
                          ~SPDR201.spl~ ~override~
                          ~SPDR202.spl~ ~override~
                          ~SPDR302.spl~ ~override~
                          ~SPDR603.spl~ ~override~
                          ~SPDR702.spl~ ~override~
                          ~SPDWD02.spl~ ~override~
                          ~SPCL940.spl~ ~override~
                          ~SPCL941.spl~ ~override~
                          ~SPCL121.spl~ ~override~
                          ~SPCL144.spl~ ~override~
                          ~SPCL152.spl~ ~override~
                          ~SPCL311.spl~ ~override~
                          ~SPCL321.spl~ ~override~
                          ~SPCL342.spl~ ~override~
                          ~SPCL412.spl~ ~override~
                          ~SPCL414.spl~ ~override~
                          ~SPCL417.spl~ ~override~
                          ~SPCL423.spl~ ~override~
                          ~SPCL52[12]+\.spl~ ~override~
                          ~SPCL61[1-3]+\.spl~ ~override~
                          ~SPCL62[1-5]+\.spl~ ~override~
                          ~SPCL63[2-4]+\.spl~ ~override~
                          ~SPCL64[134]+\.spl~ ~override~
                          ~SPCL72[12]+\.spl~ ~override~
                          ~SPCL73[12]+\.spl~ ~override~
                          ~SPCL74[12]+\.spl~ ~override~
                          ~SPCL90[0-9]+\.spl~ ~override~
                          ~SPCL91[0-4]+\.spl~ ~override~
                          ~SPCL91[6-9]+\.spl~ ~override~
                          ~SPCL92[12]+\.spl~ ~override~
                          ~SPCL93[6-8]+\.spl~ ~override~
                          ~SPCL94[0-3]+\.spl~ ~override~
                          ~SPCLI12.spl~ ~override~
                          ~SPCLI13.spl~ ~override~
                          ~SPCLI15.spl~ ~override~
                          ~SPCLI16.spl~ ~override~
                          ~SPIN001.spl~ ~override~
                          ~SPIN1[01][0-9].spl~ ~override~
                          ~SPIN12[0-7].spl~ ~override~
                          ~SPIN15[0-7].spl~ ~override~
                          ~SPIN649.spl~ ~override~
                          ~SPIN667.spl~ ~override~
                          ~SPIN71[378].spl~ ~override~
                          ~SPIN82[6-9].spl~ ~override~
                          ~SPIN853.spl~ ~override~
                          ~SPRA30[1-6].spl~ ~override~
                          ~SPSD02.spl~ ~override~
                          ~SPINI101.spl~ ~override~
                          ~SPINI2[67].spl~ ~override~
                          ~SPINI9[0-2].spl~ ~override~
                          ~OHTMPS[0-9]+\.spl~ ~override~
                          ~OHTYR[0-9]+\.spl~ ~override~

  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 321
      target = 1
      insert_point = 0
    STR_VAR
      resource = ~SPPRI25~
  END

// patch on-hit magic shield on Keldorn's items to prevent resonating shield effect //

COPY_EXISTING ~keldorn.spl~ ~override/keldorn.spl~
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode  = 12
      opcode        = 206
      parameter1    = 0
      parameter2    = 0
      timing        = 0
      duration      = 1
    STR_VAR
      insert        = ~last~
      resource      = ~keldorn~
  END
BUT_ONLY
