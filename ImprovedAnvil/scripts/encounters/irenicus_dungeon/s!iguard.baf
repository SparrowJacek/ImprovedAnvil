IF
	Detect([PC])
  Allegiance(Myself,NEUTRAL)
	Range(LastSeenBy(Myself),10)
	Global("spoke","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("spoke","LOCALS",1)
		StartDialogNoSet(LastSeenBy(Myself))
END

IF
  AttackedBy([GOODCUTOFF],DEFAULT)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_SHIELD)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_BLUR)
    ReallyForceSpell(Myself,WIZARD_GHOST_ARMOR)
    ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
    ReallyForceSpell(Myself,WIZARD_STRENGTH)
    ReallyForceSpell(Myself,WIZARD_HASTE)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("EnemySeen","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("EnemySeen","LOCALS",1)
END

IF
  Allegiance(Myself,ENEMY)
  OR(2)
    Detect([PC])
    Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,60)
  HasItem("potn08",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn08",Myself)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("EnemySeen","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",90)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("EnemySeen","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  Global("EnemySeen","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",0)
    SetGlobal("EnemySeen","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  RandomNumGT(6,4)
  OR(2)
    AttackedBy(LastSeenBy(Myself),MELEE)
    Range(LastSeenBy(Myself),2)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(ThirdNearestEnemyOf(Myself))
  RandomNumLT(6,4)
  OR(2)
    AttackedBy(LastSeenBy(Myself),MELEE)
    Range(LastSeenBy(Myself),2)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
    AttackOneRound(LastSeenBy(Myself))
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
    AttackOneRound(NearestEnemyOf(Myself))
END