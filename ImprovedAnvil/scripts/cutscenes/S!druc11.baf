// Extended druid stronghold, combat encounters through Small Teeth Pass //

// movement towards the bridge after the initial dialogue
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",0)
THEN
  RESPONSE #100
    CutSceneId(Player1)
    SetInterrupt(FALSE)

    // movement to the bridge
    // @TODO: add dram kallen's companions
    // animate the movement properly
    JumpToPoint([3120.1750])
    MoveViewObject(Myself,STANDARD)
    ActionOverride("S!tslead",JumpToPoint([2940.1710]))
    ActionOverride("S!tslead",FaceObject(Player1))
    FaceObject("S!tslead")

    ActionOverride(Player2,JumpToPoint([3245.1690]))
    ActionOverride(Player2,FaceObject("s!tslead"))
    ActionOverride(Player3,JumpToPoint([3225.1580]))
    ActionOverride(Player3,FaceObject("s!tslead"))
    ActionOverride(Player4,JumpToPoint([3080.1530]))
    ActionOverride(Player4,FaceObject("s!tslead"))
    ActionOverride(Player5,JumpToPoint([2930.1510]))
    ActionOverride(Player5,FaceObject("s!tslead"))
    ActionOverride(Player6,JumpToPoint([2980.1400]))
    ActionOverride(Player6,FaceObject("s!tslead"))

    Wait(2)

    // dialog at the bridge
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5773))
    Wait(2)
    DisplayStringHead(Myself,@5774)
    Wait(2)

    // move across the bridge
    // @TODO: animate the movement properly
    // @TODO: create and position enemies beforehand
    JumpToPoint([1660.2480])
    MoveViewObject(Myself,STANDARD)
    ActionOverride("S!tslead",JumpToPoint([1935.2490]))
    ActionOverride("S!tslead",FaceObject(Player1))

    ActionOverride(Player2,JumpToPoint([1745.2400]))
    ActionOverride(Player2,FaceObject("s!tslead"))
    ActionOverride(Player3,JumpToPoint([2050.2550]))
    ActionOverride(Player3,FaceObject("s!tslead"))
    ActionOverride(Player4,JumpToPoint([2270.2530]))
    ActionOverride(Player4,FaceObject("s!tslead"))
    ActionOverride(Player5,JumpToPoint([2210.2305]))
    ActionOverride(Player5,FaceObject("s!tslead"))
    ActionOverride(Player6,JumpToPoint([2290.2365]))
    ActionOverride(Player6,FaceObject("s!tslead"))

    Wait(2)

    SetGlobal("IAFight","AR1700",1)
    MultiPlayerSync()
    SetInterrupt(TRUE)
    EndCutSceneMode()
END

// movement after the fight at the bridge
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",2)
THEN
  RESPONSE #100
    CutSceneId(Player1)
    SetInterrupt(FALSE)

    // move towards the ambush point
    // @TODO: animate the movement properly
    // @TODO: create and position enemies beforehand
    JumpToPoint([710.2620])
    MoveViewObject(Myself,STANDARD)
    ActionOverride("S!tslead",JumpToPoint([630.2615]))
    ActionOverride("S!tslead",FaceObject(Player1))

    ActionOverride(Player2,JumpToPoint([830.2520]))
    ActionOverride(Player2,FaceObject("s!tslead"))
    ActionOverride(Player3,JumpToPoint([830.2605]))
    ActionOverride(Player3,FaceObject("s!tslead"))
    ActionOverride(Player4,JumpToPoint([690.2700]))
    ActionOverride(Player4,FaceObject("s!tslead"))
    ActionOverride(Player5,JumpToPoint([545.2685]))
    ActionOverride(Player5,FaceObject("s!tslead"))
    ActionOverride(Player6,JumpToPoint([500.2560]))
    ActionOverride(Player6,FaceObject("s!tslead"))

    Wait(2)

    // dialog before the ambush
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5775))
    Wait(2)
    DisplayStringHead(Myself,@5776)
    Wait(2)

    AddJournalEntry(@5526,QUEST)
    SetGlobal("IAFight","AR1700",3)
    MultiPlayerSync()
    SetInterrupt(TRUE)
    EndCutSceneMode()
END

// movement after the ambush, Dram Kallen survives
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",4)
  Exists("S!tslead")
  !Dead("S!tslead")
THEN
  RESPONSE #100
    CutSceneId(Player1)
    SetInterrupt(FALSE)
    MoveViewObject(Myself,STANDARD)

    ActionOverride("S!tslead",MoveToObjectNoInterrupt(Player1))
    Wait(1)

    ActionOverride("S!tslead",DisplayStringHead(Myself,@5777))
    Wait(2)
    DisplayStringHead(Myself,@5778)
    Wait(2)
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5779))
    Wait(2)
    ActionOverride("S!tslead",DisplayStringHead(Myself,@5780))
    SmallWait(2)

    ActionOverride("S!tslead",EscapeArea())

    // @TODO: the rest of dram's party leaves

    MultiPlayerSync()
    SetInterrupt(TRUE)
    EndCutSceneMode()
END

// movement after the ambush, Dram Kallen perished
IF
  Global("IADruidPlot","GLOBAL",10)
  Global("IAFight","AR1700",4)
  Exists("S!tslead")
  Dead("S!tslead")
THEN
  RESPONSE #100
    CutSceneId(Player1)
    SetInterrupt(FALSE)
    MoveViewObject(Myself,STANDARD)

    Wait(2)
    DisplayStringHead(Myself,@5781)
    Wait(2)
    DisplayStringHead(Myself,@5782)
    SmallWait(2)

    // @TODO: the rest of dram's party leaves

    MultiPlayerSync()
    SetGlobal("IADramDies","GLOBAL",1)
    SetInterrupt(TRUE)
    EndCutSceneMode()
END